class MapDescription : Actor {
	override void PostBeginPlay() {
		INIFile data;
		data.ReadLumpsNamed("DemoMapDescriptions");
		
		let description = data.Get(level.MapName, "Description");
		
		// INIFile provides a convenience method for looking up a section for the current map:
		let descriptionExtra = data.CurrentMapGet("DescriptionExtra");
		
		// Keys can also exist outside of any section.
		if (!description)
			description = data.Get("", "DefaultDescription");
		
		Console.Printf("Description for %s: %s", level.MapName, description? description : data.Get("", "Description"));
		
		if (descriptionExtra)
			Console.Printf("DescriptionExtra for %s: %s", level.MapName, descriptionExtra);
		else
			Console.Printf("No DescriptionExtra for %s.", level.MapName);
		
		Console.Printf("This map is%s awesome.", data.CurrentMapGetBool("Awesome")? "" : " not");
		
		Destroy();
	}
}

class MapDescriptionDump : Actor {
	override void PostBeginPlay() {
		INIFile data;
		data.ReadLumpsNamed("DemoMapDescriptions");
		data.DumpToConsole();
		
		Destroy();
	}
}

class ClassDescriptionDump : Actor {
	override void PostBeginPlay() {
		INIFile data;
		data.ReadLumpsNamed("DemoClassDescriptions");
		data.MergeByActorClass(purgeSuperSubSections: true, purgeNoMatch: true);
		data.DumpToConsole();
		
		Destroy();
	}
}
